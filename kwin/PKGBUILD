# Maintainer: Vlad Zagorodniy <vladzzag@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kwin
pkgname=kwin-gtk-frame-extents
pkgver=5.16.2
pkgrel=1
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kwindowsystem-gtk-frame-extents kscreenlocker xcb-util-cursor
         plasma-framework kcmutils breeze kinit qt5-sensors)
provides=("kwin=$pkgver")
conflicts=('kwin')
makedepends=(extra-cmake-modules qt5-tools kdoctools)
optdepends=('qt5-virtualkeyboard: virtual keyboard support for kwin-wayland')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$_pkgname-$pkgver.tar.xz"{,.sig}
        0001-Implement-_GTK_FRAME_EXTENTS.patch)
install=$_pkgname.install
sha256sums=('bf3e12e52d1f0917ba45f2927f3707c8631c5f9a6e3ec38029425ff28b15067f'
            'SKIP'
            'bf7b036db705b4fd3ec12cb4df6d17f5417523905cdd140437f2e02bea1cdcba')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $_pkgname-$pkgver
  patch -Np1 < "$srcdir/0001-Implement-_GTK_FRAME_EXTENTS.patch"
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
